# Convert series of stars to Roman numerals by a Thue algorithm
# Yoel Matveyev 2020
 a::
O::->.Input a series of stars\n
::
I::->.|>
B::
->.|
::
O q::|>|->NULLA
# Converting to assorted additive Roman numerals
T::
*->i
iiiii->v
vv->x
xxxxx->l
ll->c
ccccc->d
dd->m
# Ordering additive Roman numerals, setting a marker at the end
|<->|>
|>i->|i>
|>v->|v>
|>x->|x>
|>l->|l>
|>c->|c>
|>d->|d>
|>m->|m>

i>v-><vi
i>x-><xi
i>l-><li
i>c-><ci
i>d-><di
i>m-><mi
v>x-><xv
v>l-><lv
v>c-><cv
v>d-><dv
v>m-><mv
x>l-><lx
x>c-><cx
x>d-><dx
x>m-><mx
l>c-><cl
l>d-><dl
l>m-><ml
c>d-><dc
c>m-><mc
d>m-><md

i<-><i
v<-><v
x<-><x
l<-><l
c<-><c
d<-><d
m<-><m

i>i->ii>
v>i->vi>
x>i->xi>
l>i->li>
c>i->ci>
d>i->di>
m>i->mi>
v>v->x>
x>v->xv>
l>v->lv>
c>v->cv>
d>v->dv>
m>v->mv>
x>x->xx>
l>x->lx>
c>x->cx>
d>x->dx>
m>x->mx>
l>l->c>
c>l->cl>
d>l->dl>
m>l->ml>
c>c->cc>
d>c->dc>
m>c->mc>
d>d->m>
m>d->md>
m>m->mm>
# Capitalizing
>|->\.|
i.->\.I
v.->\.V
x.->\.X
l.->\.L
c.->\.C
d.->\.D
m.->\.M
# Applying the subtractive rules
IIII->IV
VIIII->IX
VIV->IX
XXXX->XL
LXXXX->XC
LXL->XC
CCCC->CD
DCCCC->CM
DCD->CM
# Output
|.->|.,
O::,M->M
O::,D->D
O::,C->C
O::,L->L
O::,X->X
O::,V->V
O::,I->I
,,->
,->
|.|->
# Try again?
 q::
->._
::
O::_->\nTry again? (y/n)
->._
::
I::_->
 a::y->
 end::n->
